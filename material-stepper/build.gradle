apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'
apply from: '../material-stepper/code_coverage.gradle'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 29
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        //workaround so that code coverage is working with Robolectric: http://stackoverflow.com/a/35769943/973379
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
        //fixes https://github.com/robolectric/robolectric/issues/3169#issuecomment-312046322
        unitTests.includeAndroidResources = true
    }

    lintOptions {
        warningsAsErrors true
        disable 'RequiredSize', 'ClickableViewAccessibility'
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.0.0'

    testImplementation "junit:junit:4.13"
    testImplementation "org.mockito:mockito-core:3.3.3"
    testImplementation "com.squareup.assertj:assertj-android:1.2.0"
    testImplementation("org.robolectric:robolectric:4.3.1") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:1.2.71"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:1.3.72"
    testImplementation "com.nhaarman:mockito-kotlin:1.6.0"
}

gradle.taskGraph.beforeTask { Task task ->
    if (task instanceof Javadoc) {
        def releaseVariant = android.libraryVariants.find { it.buildType.name == 'release' }
        Javadoc javadocTask = (Javadoc) task
        javadocTask.source = android.sourceSets.main.java.srcDirs
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        javadocTask.classpath = files(releaseVariant.javaCompile.classpath.files) + files(ext.androidJar)
    }
}

// Log out test results to console
tasks.matching { it instanceof Test}.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

task ('checkWithCoverage') {
    dependsOn 'checkstyle', 'findbugs', 'pmd', 'jacocoTestReport', 'lintDebug'
}

ext {
    artifact = POM_ARTIFACT_ID
}

apply from: '../installv1.gradle'
apply from: '../bintrayv1.gradle'

install.mustRunAfter check